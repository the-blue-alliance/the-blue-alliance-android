apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'gitsemver'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.facebook.testing.screenshot'
apply plugin: 'com.noveogroup.android.check'

apply from: "../code_coverage.gradle"

// dependency checker plugin
// run ./gradlew dependencyUpdates to generate report
apply plugin: 'com.github.ben-manes.versions'

apply from: 'versioning.gradle'
apply from: 'build.workaround-missing-resource.gradle'

// Fix for https://github.com/evant/gradle-retrolambda/issues/105
// Without this, the build fails on CI for some reason
retrolambda {
    jvmArgs '-noverify'
}

version semverVersion()
def (major, minor, patch, versionNum, gitTag, gitHash, commitsFromTag, repoDirty) =
buildVersionCode()

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        quiet.events = ["passed", "skipped"]
    }
}

project.afterEvaluate {
    def generateReleasePlayResources = project.tasks.getByName("generateProdReleasePlayResources")
    generateReleasePlayResources.dependsOn loadProperties

    def pullScreenshots = project.tasks.getByName("pullScreenshots")
    pullScreenshots.dependsOn updateScreenshotDirs
}

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    signingConfigs {
        release {
            storeFile file("somefile.jks")
            storePassword "notRealPassword"
            keyAlias "notRealAlias"
            keyPassword "notRealPassword"
        }

        productFlavors {
            dev {
                // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
                // to pre-dex each module and produce an APK that can be tested on
                // Android Lollipop without time consuming dex merging processes
                minSdkVersion 21
                multiDexEnabled true

            }
            prod {
                // to install a debug app with minSdkVersion = 16, run ./gradlew installProdDebug
                // see http://developer.android.com/tools/building/multidex.html#dev-build
                minSdkVersion 16
                multiDexEnabled true
            }
        }

        buildTypes {
            debug {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
            }

            debugProguard {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
                testProguardFile 'proguard-rules.txt'
            }

            // run 'gradlew assembleDebugBlue' to do a debug signed build without using debug resources
            // the apk will be in android/build/apk and you can install it by running
            // 'adb install -r <file name>'
            debugBlue {
                signingConfig signingConfigs.debug
                applicationIdSuffix ".development"
                manifestPlaceholders = [gcmPermissionRequired: ""]
            }

            release {
                manifestPlaceholders = [gcmPermissionRequired: "com.google.android.c2dm.permission.SEND"]
                signingConfig signingConfigs.release
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
            }
        }
    }

    defaultConfig {
        applicationId "com.thebluealliance.androidclient"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionNum
        versionName version.toString()
        multiDexEnabled true

        testInstrumentationRunner "com.thebluealliance.androidclient.testing.TbaInstrumentationRunner"

        // Needed for support lib 23.2.0+ on pre-L devices
        vectorDrawables.useSupportLibrary = true

        // Add specific versioning things to BuildConfig
        buildConfigField "int", "MAJOR_VERSION", "$major"
        buildConfigField "int", "MINOR_VERSION", "$minor"
        buildConfigField "int", "PATCH_VERSION", "$patch"
        buildConfigField "String", "GIT_TAG", "\"$gitTag\""
        buildConfigField "String", "GIT_HASH", "\"$gitHash\""
        buildConfigField "int", "COMMITS_FROM_TAG", "$commitsFromTag"
        buildConfigField "boolean", "REPO_DIRTY", "$repoDirty"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Don't throw dreaded stub exceptions
        unitTests.returnDefaultValues = true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apkName = "tba-android-";
            apkName += "v" + version.tagName;
            apkName += "-" + variant.buildType.name + ".apk";
            output.outputFile = file("$project.buildDir/apk/" + apkName)
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debugBlue {
            debuggable true
        }
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "2048M"
        preDexLibraries = preDexEnabled && !travisBuild
    }

}

play {
    serviceAccountEmail = 'some-email-address'
    pk12File = file("somefile.p12")
    track = 'beta'
}

screenshots {
    testApkTarget = 'packageDevDebugAndroidTest'
    connectedAndroidTestTarget = 'connectedDevDebugAndroidTest'
}

println "Version: " + android.defaultConfig.versionName + "/" + android.defaultConfig.versionCode

task loadProperties << {
    // add in signing keys from local.properties
    def config = new Properties()
    def propFile = file("../local.properties")
    System.out.println("Loading property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        android.signingConfigs.release.storeFile = file(config["release.key"])
        android.signingConfigs.release.storePassword = config["release.key.password"]
        android.signingConfigs.release.keyAlias = config["release.key.alias"]
        android.signingConfigs.release.keyPassword = config["release.key.aliasPass"]

        play.serviceAccountEmail = config['play.release.serviceEmail']
        play.pk12File = file(config['play.release.keyFile'])
    }
}

task updateScreenshotDirs << {
    def codeNameOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.product.name'
        standardOutput codeNameOut
    }

    def sdkOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.build.version.sdk'
        standardOutput sdkOut
    }
    def deviceName = codeNameOut.toString().trim()
    def deviceSdk = sdkOut.toString().trim()
    def outputDir = project.screenshots.recordDir + "/" + deviceName + "_api" + deviceSdk
    project.screenshots.recordDir = outputDir
    System.out.println("New screenshot output dierctory: " + outputDir)

}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageDevRelease")
            || theTask.name.equals("packageProdRelease")
            || theTask.name.equals("publishProdRelease")
            || theTask.name.equals("bootstrapProdReleasePlayResources")
            || theTask.name.equals("publishListingProdRelease")
            || theTask.name.equals("publishApkProdRelease")) {
        theTask.dependsOn "loadProperties"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "http://github.com/wada811/Android-Material-Design-Colors/raw/master/repository/"
    }
}

check {
    abortOnError true
    checkstyle {
        config project.file('../config/checkstyle.xml')
    }
}

dependencies {
    compile project(':libTba')
    compile project(':libImgur')

    // Android support libraries
    compile "com.android.support:support-v13:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${supportLibVersion}"
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:design:${supportLibVersion}"

    // Play Services Libraries
    // See http://developer.android.com/google/play-services/setup.html
    compile 'com.google.collections:google-collections:1.0'
    compile "com.google.android.gms:play-services-base:${playServicesVersion}"
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    compile "com.google.android.gms:play-services-gcm:${playServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${playServicesVersion}"
    compile "com.google.firebase:firebase-core:${playServicesVersion}"
    compile "com.google.firebase:firebase-auth:${playServicesVersion}"
    compile "com.google.firebase:firebase-config:${playServicesVersion}"

    debugCompile "com.github.phil-lopreiato:firebasecrash-noop:v0.1"
    debugBlueCompile "com.github.phil-lopreiato:firebasecrash-noop:v0.1"
    debugProguardCompile "com.github.phil-lopreiato:firebasecrash-noop:v0.1"
    testCompile 'com.github.phil-lopreiato:firebasecrash-noop:v0.1'
    releaseCompile "com.google.firebase:firebase-crash:${playServicesVersion}"

    // Square Libraries
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    compile "com.google.dagger:dagger:${daggerVersion}"
    apt 'com.google.guava:guava:19.0'
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"

    // Other third party libraries
    compile "com.google.code.gson:gson:${gsonVersion}"
    compile 'me.xuender:unidecode:0.0.7'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.facebook.stetho:stetho:1.4.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.1'
    compile 'com.firebase:firebase-client-android:2.5.2'
    compile "io.reactivex:rxandroid:${rxAndroidVersion}"
    compile "io.reactivex:rxjava:${rxJavaVersion}"
    compile 'io.reactivex:rxjava-math:1.0.0'
    compile 'me.relex:circleindicator:1.2.1@aar'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'io.nlopez.smartadapters:library:1.3.1'
    compile 'com.wada811:android-material-design-colors:3.0.0'
    compile 'com.thebluealliance:spectrum:0.6.0'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile "com.github.hotchemi:permissionsdispatcher:${permissionDispatcherVersion}"
    apt "com.github.hotchemi:permissionsdispatcher-processor:${permissionDispatcherVersion}"

    // testing
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "org.robolectric:shadows-support-v4:${robolectricVersion}"
    testCompile "org.robolectric:shadows-multidex:${robolectricVersion}"
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testApt "com.google.guava:guava:19.0"
    testApt "com.google.dagger:dagger-compiler:${daggerVersion}"

    // instrumentation
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'

    // Leak Canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugBlueCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugProguardCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
}

apply plugin: 'com.google.gms.google-services'