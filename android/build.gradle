apply plugin: 'com.android.application'
apply plugin: 'gitsemver'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.facebook.testing.screenshot'
apply plugin: 'com.noveogroup.android.check'

apply from: "../code_coverage.gradle"

// dependency checker plugin
// run ./gradlew dependencyUpdates to generate report
apply plugin: 'com.github.ben-manes.versions'

apply from: 'versioning.gradle'

// Does `testOptions.unitTests.includeAndroidResources = true` obviate this workaround?
apply from: 'build.workaround-missing-resource.gradle'

version semverVersion()
def (major, minor, patch, versionNum, gitTag, gitHash, commitsFromTag, repoDirty) =
buildVersionCode()

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        quiet.events = ["passed", "skipped"]
    }
}

project.afterEvaluate {
    def pullScreenshots = project.tasks.getByName("pullScreenshots")
    pullScreenshots.dependsOn updateScreenshotDirs
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    signingConfigs {
        release {
            storeFile file("somefile.jks")
            storePassword "notRealPassword"
            keyAlias "notRealAlias"
            keyPassword "notRealPassword"
        }

        buildTypes {
            debug {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
            }

            debugProguard {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
                testProguardFile 'proguard-rules.txt'
                // Alternate library build type matches for this build type. See
                // https://developer.android.com/studio/build/gradle-plugin-3-0-0-migration.htm
                matchingFallbacks = ['debug', 'release']
            }

            // run 'gradlew assembleDebugBlue' to do a debug signed build without using debug resources
            // the apk will be in android/build/apk and you can install it by running
            // 'adb install -r <file name>'
            debugBlue {
                signingConfig signingConfigs.debug
                applicationIdSuffix ".development"
                manifestPlaceholders = [gcmPermissionRequired: ""]
                matchingFallbacks = ['debug', 'release']
            }

            release {
                manifestPlaceholders = [gcmPermissionRequired: "com.google.android.c2dm.permission.SEND"]
                signingConfig signingConfigs.release
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
            }
        }
    }

    defaultConfig {
        applicationId "com.thebluealliance.androidclient"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode versionNum
        versionName version.toString()
        multiDexEnabled true

        testInstrumentationRunner "com.thebluealliance.androidclient.testing.TbaInstrumentationRunner"

        // Needed for support lib 23.2.0+ on pre-L devices
        vectorDrawables.useSupportLibrary = true

        // Add specific versioning things to BuildConfig
        buildConfigField "int", "MAJOR_VERSION", "$major"
        buildConfigField "int", "MINOR_VERSION", "$minor"
        buildConfigField "int", "PATCH_VERSION", "$patch"
        buildConfigField "String", "GIT_TAG", "\"$gitTag\""
        buildConfigField "String", "GIT_HASH", "\"$gitHash\""
        buildConfigField "int", "COMMITS_FROM_TAG", "$commitsFromTag"
        buildConfigField "boolean", "REPO_DIRTY", "$repoDirty"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Don't throw dreaded stub exceptions
        unitTests.returnDefaultValues = true

        // Do resource, asset, and manifest merging before running your unit
        // tests that require Android resources, such as Roboelectric
        testOptions.unitTests.includeAndroidResources = true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def apkName = "tba-android-";
            apkName += "v" + version.tagName;
            apkName += "-" + variant.buildType.name + ".apk";
            output.outputFileName = apkName
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debugBlue {
            debuggable true
        }
    }

    dexOptions {
        javaMaxHeapSize "2048M"
        preDexLibraries = preDexEnabled && !travisBuild
    }

}

play {
    serviceAccountEmail = 'some-email-address'
    pk12File = file("somefile.p12")
    track = 'beta'
}

screenshots {
    testApkTarget = 'packageDevDebugAndroidTest'
    connectedAndroidTestTarget = 'connectedDevDebugAndroidTest'
}

println "Version: " + android.defaultConfig.versionName + "/" + android.defaultConfig.versionCode

task loadProperties << {
    // add in signing keys from local.properties
    def config = new Properties()
    def propFile = file("../local.properties")
    System.out.println("Loading property file: " + propFile.absolutePath)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        android.signingConfigs.release.storeFile = file(config["release.key"])
        android.signingConfigs.release.storePassword = config["release.key.password"]
        android.signingConfigs.release.keyAlias = config["release.key.alias"]
        android.signingConfigs.release.keyPassword = config["release.key.aliasPass"]

        play.serviceAccountEmail = config['play.release.serviceEmail']
        play.pk12File = file(config['play.release.keyFile'])
    }
}

task updateScreenshotDirs << {
    def codeNameOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.product.name'
        standardOutput codeNameOut
    }

    def sdkOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.build.version.sdk'
        standardOutput sdkOut
    }
    def deviceName = codeNameOut.toString().trim()
    def deviceSdk = sdkOut.toString().trim()
    def outputDir = project.screenshots.recordDir + "/" + deviceName + "_api" + deviceSdk
    project.screenshots.recordDir = outputDir
    System.out.println("New screenshot output dierctory: " + outputDir)

}

tasks.whenTaskAdded { theTask ->
    if (theTask.name in ["publishApkRelease", "publishListingRelease", "publishRelease",
                         "bootstrapReleasePlayResources",
                         "generateReleasePlayResources"]) {
        theTask.dependsOn "loadProperties"
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "http://github.com/wada811/Android-Material-Design-Colors/raw/master/repository/"
    }
}

check {
    abortOnError true
    checkstyle {
        config project.file('../config/checkstyle.xml')
    }
}

dependencies {
    implementation project(':libTba')
    implementation project(':libImgur')

    // Android support libraries
    implementation "com.android.support:support-v13:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:gridlayout-v7:${supportLibVersion}"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:design:${supportLibVersion}"

    // Play Services Libraries
    // See http://developer.android.com/google/play-services/setup.html
    implementation 'com.google.guava:guava:22.0'
    implementation "com.google.android.gms:play-services-base:${playServicesVersion}"
    implementation "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    implementation "com.google.android.gms:play-services-gcm:${playServicesVersion}"
    implementation "com.google.android.gms:play-services-auth:${playServicesVersion}"
    implementation "com.google.firebase:firebase-core:${playServicesVersion}"
    implementation "com.google.firebase:firebase-auth:${playServicesVersion}"
    implementation "com.google.firebase:firebase-config:${playServicesVersion}"

    testImplementation 'com.github.phil-lopreiato:firebasecrash-noop:v0.1'
    releaseImplementation "com.google.firebase:firebase-crash:${playServicesVersion}"
    debugImplementation "com.google.firebase:firebase-crash:${playServicesVersion}"
    debugBlueImplementation "com.google.firebase:firebase-crash:${playServicesVersion}"
    debugProguardImplementation "com.google.firebase:firebase-crash:${playServicesVersion}"

    // Square Libraries
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'

    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor 'com.google.guava:guava:22.0'
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // Other third party libraries
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation 'me.xuender:unidecode:0.0.7'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.facebook.stetho:stetho:1.4.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.4.1'
    implementation 'com.firebase:firebase-client-android:2.5.2'
    implementation "io.reactivex:rxandroid:${rxAndroidVersion}"
    implementation "io.reactivex:rxjava:${rxJavaVersion}"
    implementation 'io.reactivex:rxjava-math:1.0.0'
    implementation 'me.relex:circleindicator:1.2.1@aar'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'io.nlopez.smartadapters:library:1.3.1'
    implementation 'com.wada811:android-material-design-colors:3.0.0'
    implementation 'com.thebluealliance:spectrum:0.7.1'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.github.hotchemi:permissionsdispatcher:${permissionDispatcherVersion}"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:${permissionDispatcherVersion}"

    // testing
    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation "org.robolectric:shadows-support-v4:${robolectricVersion}"
    testImplementation "org.robolectric:shadows-multidex:${robolectricVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    testAnnotationProcessor 'com.jakewharton:butterknife:7.0.1'

    // instrumentation
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugBlueImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugProguardImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
}

apply plugin: 'com.google.gms.google-services'