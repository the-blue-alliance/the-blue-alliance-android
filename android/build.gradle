apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'gitsemver'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.facebook.testing.screenshot'

apply from: "../code_coverage.gradle"

// dependency checker plugin
// run ./gradlew dependencyUpdates to generate report
apply plugin: 'com.github.ben-manes.versions'

apply from: 'versioning.gradle'
apply from: 'build.workaround-missing-resource.gradle'

version semverVersion()
def (major, minor, patch, versionNum, gitTag, gitHash, commitsFromTag, repoDirty) =
buildVersionCode()

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        quiet.events = ["passed", "skipped"]
    }
}

project.afterEvaluate {
    def pullScreenshots = project.tasks.getByName("pullScreenshots")
    pullScreenshots.dependsOn updateScreenshotDirs
}

def localProps = new Properties()
def localPropFile = file("../local.properties")
if (localPropFile.exists()) {
    System.out.println("Loading property file: " + localPropFile.absolutePath)
    localProps.load(new FileInputStream(localPropFile))
} else {
    System.out.println("Falling back to default stub properties")
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    signingConfigs {
        release {
            storeFile file(localProps.getProperty("release.key", "somefile.jks"))
            storePassword localProps.getProperty("release.key.password", "notRealPassword")
            keyAlias localProps.getProperty("release.key.alias", "notRealAlias")
            keyPassword localProps.getProperty("release.key.aliasPass", "notRealPassword")
        }

        buildTypes {
            debug {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
            }

            debugProguard {
                applicationIdSuffix ".development"
                testCoverageEnabled true
                manifestPlaceholders = [gcmPermissionRequired: ""] // "" => let GCMBroadcastReceiver accept Intents from 'adb shell am broadcast'
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
                testProguardFile 'proguard-rules.txt'
            }

            // run 'gradlew assembleDebugBlue' to do a debug signed build without using debug resources
            // the apk will be in android/build/apk and you can install it by running
            // 'adb install -r <file name>'
            debugBlue {
                signingConfig signingConfigs.debug
                applicationIdSuffix ".development"
                manifestPlaceholders = [gcmPermissionRequired: ""]
            }

            release {
                manifestPlaceholders = [gcmPermissionRequired: "com.google.android.c2dm.permission.SEND"]
                signingConfig signingConfigs.release
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles 'proguard-rules.txt'
            }
        }
    }

    defaultConfig {
        applicationId "com.thebluealliance.androidclient"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode versionNum
        versionName version.toString()
        multiDexEnabled true

        testInstrumentationRunner "com.thebluealliance.androidclient.testing.TbaInstrumentationRunner"

        // Needed for support lib 23.2.0+ on pre-L devices
        vectorDrawables.useSupportLibrary = true

        // Add specific versioning things to BuildConfig
        buildConfigField "int", "MAJOR_VERSION", "$major"
        buildConfigField "int", "MINOR_VERSION", "$minor"
        buildConfigField "int", "PATCH_VERSION", "$patch"
        buildConfigField "String", "GIT_TAG", "\"$gitTag\""
        buildConfigField "String", "GIT_HASH", "\"$gitHash\""
        buildConfigField "int", "COMMITS_FROM_TAG", "$commitsFromTag"
        buildConfigField "boolean", "REPO_DIRTY", "$repoDirty"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Don't throw dreaded stub exceptions
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            // This is needed for robolectric - see
            // https://github.com/robolectric/robolectric-gradle-plugin/issues/144
            jvmArgs '-noverify'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def apkName = "tba-android-";
            apkName += "v" + version.tagName;
            apkName += "-" + variant.buildType.name + ".apk";
            output.outputFileName = apkName
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debugBlue {
            debuggable true
        }
    }

    dexOptions {
        javaMaxHeapSize "2048M"
        preDexLibraries = preDexEnabled && !travisBuild
    }

}

play {
    serviceAccountEmail = localProps.getProperty('play.release.serviceEmail', 'some-email-address')
    pk12File = file(localProps.getProperty('play.release.keyFile', 'somefile.p12'))
    track = 'beta'
}

screenshots {
    testApkTarget = 'packageDebugAndroidTest'
    connectedAndroidTestTarget = 'connectedDebugAndroidTest'
}

println "Version: " + android.defaultConfig.versionName + "/" + android.defaultConfig.versionCode

task updateScreenshotDirs << {
    def codeNameOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.product.name'
        standardOutput codeNameOut
    }

    def sdkOut = new ByteArrayOutputStream()
    exec {
        commandLine 'adb', 'shell', 'getprop', 'ro.build.version.sdk'
        standardOutput sdkOut
    }
    def deviceName = codeNameOut.toString().trim()
    def deviceSdk = sdkOut.toString().trim()
    def outputDir = project.screenshots.recordDir + "/" + deviceName + "_api" + deviceSdk
    project.screenshots.recordDir = outputDir
    System.out.println("New screenshot output dierctory: " + outputDir)

}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url "http://github.com/wada811/Android-Material-Design-Colors/raw/master/repository/"
    }
}

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    // empty classpath
    classpath = files()
}

dependencies {
    implementation project(':libTba')
    implementation project(':libImgur')
    implementation project(':spectrum')

    // Android support libraries
    implementation "com.android.support:support-v13:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:gridlayout-v7:${supportLibVersion}"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:design:${supportLibVersion}"

    // Play Services Libraries
    // See http://developer.android.com/google/play-services/setup.html
    implementation 'com.google.guava:guava:24.1-jre'
    implementation "com.google.android.gms:play-services-base:16.0.1"
    implementation "com.google.android.gms:play-services-analytics:16.0.6"
    implementation "com.google.android.gms:play-services-gcm:16.0.0"
    implementation "com.google.android.gms:play-services-auth:16.0.1"
    implementation "com.google.android.gms:play-services-flags:16.0.1"
    implementation "com.google.android.gms:play-services-stats:16.0.1"
    implementation "com.google.firebase:firebase-core:16.0.6"
    implementation "com.google.firebase:firebase-auth:16.1.0"
    implementation "com.google.firebase:firebase-config:16.1.2"

    testImplementation 'com.github.phil-lopreiato:firebasecrash-noop:v0.1'
    releaseImplementation "com.google.firebase:firebase-crash:${firebaseCrashVersion}"
    debugImplementation "com.google.firebase:firebase-crash:${firebaseCrashVersion}"
    debugBlueImplementation "com.google.firebase:firebase-crash:${firebaseCrashVersion}"
    debugProguardImplementation "com.google.firebase:firebase-crash:${firebaseCrashVersion}"

    // Square Libraries
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // Other third party libraries
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation 'me.xuender:unidecode:0.0.7'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.facebook.stetho:stetho:1.4.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.4.1'
    implementation 'com.firebase:firebase-client-android:2.5.2'
    implementation "io.reactivex:rxandroid:${rxAndroidVersion}"
    implementation "io.reactivex:rxjava:${rxJavaVersion}"
    implementation 'io.reactivex:rxjava-math:1.0.0'
    implementation 'me.relex:circleindicator:1.2.1@aar'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'io.nlopez.smartadapters:library:1.3.1'
    implementation 'com.wada811:android-material-design-colors:3.0.0'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.github.hotchemi:permissionsdispatcher:${permissionDispatcherVersion}"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:${permissionDispatcherVersion}"

    // testing
    testImplementation "org.robolectric:robolectric:${robolectricVersion}"
    testImplementation "org.robolectric:shadows-support-v4:3.3.2"
    testImplementation "org.robolectric:shadows-multidex:${robolectricVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // instrumentation
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugBlueImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    debugProguardImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
}

apply plugin: 'com.google.gms.google-services'