// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'com.google.gms:google-services:4.4.0'
        classpath 'com.google.firebase:perf-plugin:1.4.2'

        // code coverage testing
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'

        // versioning
        classpath files('libs/gradle-gitsemver-0.6.0.jar')

        // Screenshot testing
        classpath 'com.facebook.testing.screenshot:plugin:0.15.0'

        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'

        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.50'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.42.0"
    id "org.openapi.generator" version "7.10.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    ext {
        playServicesVersion = '16.0.1'
        firebaseVersion = '16.0.6'
        crashlyticsVersion = '2.10.1'
        googleApiClientVersion = '1.21.0'
        mockitoVersion = '5.8.0'
        retrofitVersion = '2.9.0'
        rxJavaVersion = '1.3.8'
        rxAndroidVersion = '1.2.1'
        gsonVersion = '2.10.1'
        daggerVersion = '2.50'
        robolectricVersion = '4.14.1'
        permissionDispatcherVersion = '4.9.2'
        workManagerVersion = '2.9.0'

        travisBuild = System.getenv("CI") == "true"
        // allows for -Dpre-dex=false to be set
        preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
    }
}

if (System.env.CI == 'true') {
    allprojects {
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
            maxHeapSize = '768m'
            forkEvery = 100   // helps when tests leak memory
            afterSuite {
                System.out.print('.')
                System.out.flush()
            }
        }
    }
}

if (System.env.robolectric_offline == 'true') {
    allprojects {
        tasks.withType(Test) {
            systemProperty 'robolectric.offline', true
            systemProperty 'robolectric.dependency.dir', './build/output/libs/'
            systemProperty 'robolectric.logging.enabled', true
        }
    }
}

