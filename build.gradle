// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://dl.bintray.com/palantir/releases"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.gms:google-services:4.2.0'

        // dependency analysis
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'

        // code coverage testing
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'

        // versioning
        classpath 'com.palantir:gradle-gitsemver:0.6.0'

        // auto-push releases
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5'

        // Retrolambda + Lint fixes
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'

        // Automated Screenshot Testing
        classpath 'com.facebook.testing.screenshot:plugin:0.4.2'

        // Static code check things
        classpath 'com.noveogroup.android:check:1.2.4'
    }

    // Exclude the linter version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }

    ext {
        supportLibVersion = '28.0.0'
        playServicesVersion = '16.0.1'
        firebaseVersion = '16.0.6'
        firebaseCrashVersion = '16.2.1'
        googleApiClientVersion = '1.21.0'
        mockitoVersion = '2.2.1'
        retrofitVersion = '2.2.0'
        rxJavaVersion = '1.2.7'
        rxAndroidVersion = '1.2.1'
        gsonVersion = '2.8.0'
        daggerVersion = '2.7'
        leakCanaryVersion = '1.5'
        robolectricVersion = '4.1'
        permissionDispatcherVersion = '2.2.0'

        travisBuild = System.getenv("TRAVIS") == "true"
        // allows for -Dpre-dex=false to be set
        preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
            maxHeapSize = '768m'
            forkEvery = 100   // helps when tests leak memory
            afterSuite {
                System.out.print('.')
                System.out.flush()
            }
        }
    }
}

