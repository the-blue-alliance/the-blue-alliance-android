// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:perf-plugin:1.4.2'

        // code coverage testing
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'

        // versioning
        classpath files('libs/gradle-gitsemver-0.6.0.jar')

        // Screenshot testing
        classpath 'com.facebook.testing.screenshot:plugin:0.14.0'

        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'

        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.41'
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.42.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    ext {
        playServicesVersion = '16.0.1'
        firebaseVersion = '16.0.6'
        crashlyticsVersion = '2.10.1'
        googleApiClientVersion = '1.21.0'
        mockitoVersion = '2.27.0'
        retrofitVersion = '2.5.0'
        rxJavaVersion = '1.2.7'
        rxAndroidVersion = '1.2.1'
        gsonVersion = '2.8.9'
        daggerVersion = '2.41'
        robolectricVersion = '4.7.3'
        permissionDispatcherVersion = '4.9.1'
        workManagerVersion = '2.8.1'

        travisBuild = System.getenv("CI") == "true"
        // allows for -Dpre-dex=false to be set
        preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
    }
}

if (System.env.CI == 'true') {
    allprojects {
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
            maxHeapSize = '768m'
            forkEvery = 100   // helps when tests leak memory
            afterSuite {
                System.out.print('.')
                System.out.flush()
            }
        }
    }
}

if (System.env.robolectric_offline == 'true') {
    allprojects {
        tasks.withType(Test) {
            systemProperty 'robolectric.offline', true
            systemProperty 'robolectric.dependency.dir', './build/output/libs/'
            systemProperty 'robolectric.logging.enabled', true
        }
    }
}

